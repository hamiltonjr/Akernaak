10 REM ************************************************
20 REM *                  AKERNAAK                    *
30 REM *             C.P. COMPUT.PESSOAIS             *
40 REM *               CP 300 / CP 500                *
50 REM ************************************************
60 RANDOM: CLS: CLEAR 2000: DEFINT B-Z: DEFSTR A
70 DIM MA(30), FP(30), VOC$(14), OBJ$(30), LO$(19), D(19,1), CF%(10), BL(10)
80 FOR N=0 TO 14: READ VOC$(N): NEXT N
82 FOR N=0 TO 30: READ OBJ$(N): NEXT N
84 FOR N=0 TO 19: READ LO$(N): NEXT N
90 FOR N=0 TO 30: LET MA(N)=RND(15)-1: NEXT N
92 FOR N=15 TO 19: LET D(N,0)=-1: LET D(N,1)=-1: NEXT N: LET D(16,0)=RND(15)-1: LET D(16,1)=RND(15)-1
100 FOR N=24 TO 30: LET MA(N)=-1: NEXT N
102 LET MA(2)=8: LET MA(16)=13: LET MA(15)=4: LET MA(19)=-1: LET MA(4)=15: LET MA(6)=16: LET K=RND(2)+14
104 LET MA(7)=-1: LET MA(11)=K: LET MA(3)=31-K: LET MA(13)=-1: LET MA(17)=-1: LET MA(27)=19: LET MA(7)=18
106 LET MA(28)=18
110 FOR N=0 TO 10: READ BL(N): NEXT N
120 LET VE$="VOCE ESTÁ ": LET VO$="VOCÊ OUVE "
130 FOR N=0 TO 14
140 LET D(N,0)=RND(15)-1: LET D(N,1)=RND(15)-1
142 NEXT N
150 DATA FUJA, LUTE, PEGUE, SAIA, AJUDA, FALE, ABRA, ENTRE, LISTE, TOQUE, DESCANSE, DESISTA, RIA, CHORE, MORRA
160 DATA A ESPADA, " ESCUDO", " LIVRO", "A CAPA", " DIAMANTE", " ANEL", " ESPELHO", " PERGAMINHO"
162 DATA " A MOEDA DE OURO", " A LANTERNA", " A FLAUTA", " ELMO", " A GARRAFA DE VINHO", " CETRO DE PRATA" 
104 DATA A CHAVE, A ARCA, A ESTÁTUA, A PASSAGEM, " ALTAR DE AKERNAAK", A ESFERA DOS ELFOS"
170 DATA " DUENDE", " OGRO", " TIGRE", " ELFO", " PITON", " MAGO", " GUERREIRO AKERNAAK"
172 DATA " FEITICEIRO DE FOGO", " MESTRE-FEITICEIRO", " ASTERION", " DRAGAO"
180 DATA "A ENTRADA DA CASA", "O HALL", "O CORREDOR", "A ESCADA", "O QUARTO"
190 DATA "A GARAGEM", "A SALA", "O SALAO DE JOGOS", "A BIBLIOTECA", "A PORTA DOS FUNDOS"
200 DATA "A DESPENSA", "A COZINHA", "O BANHEIRO", "O GRAMAD0", "O MURO"
210 DATA "A ARCA", "A PASSAGEM SECRETA", "O CASTELO VERMELHO", "O DIAMANTE", "O CAMPO ZERO"
220 DATA 3, 6, 7, 1, 6, 2, 6, 3, 3, 2, 3
230 PRINT STRING$(60,"=")
240 PRINT TAB(20);"A CASA DE AKERNAAK"
250 PRINT STRING$(60,"=")
260 PRINT"VOCE VOLTOU PARA CASA APOS UM DIA DE TRABALHO. AO CHEGAR NA ENTRADA DA CASA, POREM, VOCE SENTIU QUE TUDO SE TORCIA A SUA VOLTA. ERA UMA BARREIRA AKERNAAK..."
270 PRINT"OS ADORADORES DE AKERNAAK TOMARAM POSSE DE SUA CASA E O ISOLARAM DO UNIVERSO."
280 PRINT"QUANDO VOCE NAO SOUBER O QUE FAZER, INTRODUZA 'AJUDA'"
290 LET P=0
300 PRINT VE$;"N";LO$(P)
310 LET FE=0: FOR N=0 TO 30: IF MA(N)<>P THEN GOTO 330 ELSE IF NOT FE THEN PRINT"AQUI HÁ: ";: LET FE=-1
320 PRINT"UM";OBJ$(N);",";
330 NEXT N: IF FE THEN PRINT CHR$(8)
340 IF D(P,0)=-1 OR D(P,1)=-1 THEN 360
350 PRINT"VOCÊ PODE SE MOVER PARA ";LO$(D(P,0));" OU PARA ";LO$(D(P,1))
360 IF CF<-3 THEN PRINT VE$;: IF CF<-6 THEN PRINT"FERIDO" ELSE PRINT"CANSADO" ELSE IF CF>1 THEN PRINT VE$;"SUPER-RESISTENTE"
370 INPUT A: FOR N=0 TO 14:IF MID$(LO$(N),3,LEN(A))=A THEN 440 ELSE NEXT N
380 FOR N=0 TO 14: IF VOC$(N)<>A THEN NEXT N: GOTO400
390 ON N+1 GOTO 1060, 940, 790, 1400, 550, 610, 1230, 1480, 870, 1010, 1140, 2230
400 PRINT"NÃO ENTENDI ISTO": GOTO370
410 REM************************************************
420 REM*                  MOIMENTAÇÃO                 *
430 REM'************************************************
440 IF FB>0 THEN 480
450 IF D(P,0)=N OR D(P,1)=N THEN LET P=N ELSE 490
460 IF FS>0 THEN LET MA(FS)=P: LET FS=0
470 GOTO1560
480 LET N=FB: PRINT"O";OBJ$(N);"PULA SOBRE VOCÊ!": GOSUB1850: GOTO1560
500 PRINT"NÃO HÁ PASSAGEM PARA ";LO$(N)
510 GOTO 1560
520 REM ************************************************
530 REM *                     AJUDA                    *
540 REM ************************************************
550 PRINT"O SEU OBJETIVO É LIVERTAR SUA CASA DOS AKERNAAKEN. PARA SE MOVER, USE O NOME DO LOCAL PARA ONDE QUISER IR.EX: 'GARAGEM'"
560 PRINT"SEU VOCABULÁRIO É ": FOR N=0 TO 31: PRINT VOC$(N);",";: NEXT N: PRINT
570 GOTO 1560
580 REM ************************************************
590 REM *                      FALE                    *
600 REM ************************************************
610 INPUT"O QUE";A:PRINT"--";A;"!!": IF A="AKON" THEN 660
620 IF VAL(A)<>0 THEN IF VAL(A)=RN THEN RN=0: PRINT VE$;"SUPER-RESISTENTE POR ALGUM TEMPO": LET CF=CF+5: GOTO 1560 ELSE PRINT"NÃO DIGA QUALQUER NÚMERO NUMA CASA DE AKERNAAK!": LET CF=CF-8: GOTO1560
630 IF A=PM$ AND MA(30)=P THEN 2200
640 PRINT"NADA ACONTECE..."
650 GOTO 1560
660 IF RND(4)>FA THEN LET FA=FA+1: GOTO750
670 LET FA=FA+1: IF RND(3)=3 THEN 720
680 PRINT"A BARREIRA SOFREU UMA SATURAÇÃO... O UNIVERSO PODERIA TER SIDO DESTRUÍDO COM ESSE USO DE PALAVRAS MÁGICAS. FELIZMENTE SÓ VOCÊ SOFREU."
690 PRINT"A CONSEQUÊNCIA DE SUA IMPRUDÊNCIA: SURGE UM DOS SETE ASTERIONS..."
700 LET MA(29)=P: LET BL%(9)=8
710 GOTO 1560
720 PRINT"A PALAVRA 'AKON' ORIGINOU-SE NA PRIMEIRA BARALHA ENTRE AKERNAAK E AS FORÇAS DO BEM CHEFIADAS POR WORIEL. "
725 PRINT"AO PERCEBER QUE A BATALHA NÃO TERMINARIA, WORIEL PRONUNCIOU-A CINCO VEZES. O CAOS GERADO DEU ORIGWM AO UNIVERSO."
730 PRINT"DECIDIRAM ENTÃO QUE ESTA PALAVRA SÓ SRIA DITA NO FIM DOS TEMPOS..."
740 PRINT"VOCÊ DESTRUIU O UNIVERSO...": GOTO 2270
750 LET D(P,0)=RND(15)-1: LET D(P,1)=RND(15)-1: IF D(P,0)=D(P,1) OR D(P,0)=P OR D(P,1)=P THEN GOTO 750 ELSE PRINT"VOCÊ CONSEGUIU TORCER A BERREIRA NESSE PONTO":GOTO1560
760 REM ************************************************
770 REM *                     PEGUE                    *
780 REM ************************************************
790 INPUT"O QUE";A: FOR N=0 TO 30: GOSUB 2220: IF EX THEN 820 ELSE NEXT N
800 GOTO 950
810 GOTO 1560
820 IF N>4 THEN IF N>19 THEN PRINTVE$;"DOIDO OU VOCÊ NÃO SABE O QUE É UM ";OBJ$(N);"?": GOTO 1560 ELSE PRINT"VOCÊ NÃO PODE CARREGAR UM ";OBJ$(N): GOTO 1560
830 LET FP(N)=-1 :LET MA(N)=-1: GOTO 870
840 REM ************************************************
850 REM *                     LISTE                    *
860 REM ************************************************
870 LET FE=0: FOR N=0 TO 14: IF NOT FP(N) THEN 890 ELSE ELSE IF NOT FE THEN PRINTVE$;"LEVANDO:";:FE=-1
880 PRINT"UM";OBJ$(N);",";
890 NEXT N: IF FE THEN PRINT CHR$(8) ELSE PRINT"VOCÊ NÃO CARREGA NADA"
900 GOTO 1560
910 REM ************************************************
920 REM *                     LUTE                     *
930 REM ************************************************
940 INPUT"COM QUEM";A:FOR N=0 TO 30: GOSUB 2220: IF EX THEN 960 ELSE NEXT N
950 PRINT"NÃO HÁ ";A;" AQUI": GOTO 1560
960 IF N<20 THEN PRINT"VOCÊ QUER LUTAR CONTRA UM ";OBJ$(N);"???": GOTO 1560
970 LET CB=2: GOSUB 1850: GOTO 1560
980 REM ************************************************
990 REM *                     TOQUE                    *
1000 REM ***********************************************
1010 IF NOT FP(10) THEN PRINT"VOCÊ NÃO LEVA NENHUM INSTRUMENTO" ELSE PRINT"VOCÊ COMEÇA A TOCAR A FLAUTA": LET FT=-1
1020 GOTO 1560
1030 REM ************************************************
1040 REM *                     FUJA                     *
1050 REM ************************************************
1060 INPUT"PARA ONDE";A: FOR N=0 TO 19: IF MID$(LO$(N),3,LEN(A))<>A THEN NEXT N: PRINT"O QUE É ISSO?": GOTO 1060 ELSE IF N>14 THEN 1490
1070 IF FB=0 THEN 450
1080 IF RND(5)=3 THEN LET CB=-2: LET N=FB: PRINT"VOCÊ TROPEÇA E O ";OBJ(N);" O ATACA PELAS COSTAS": GOSUB 1850: GOTO 1590
1090 IF RND(3)<3 THEN PRINT"O";OBJ$(FB);" O SEGUE": LET FS=FB: LET FB=0: GOTO 450
1100 LET FB=0: GOTO 450
1110 REM ************************************************
1120 REM *                  DESCANSE                    *
1130 REM ************************************************
1140 LET FE=0: PRINT"VOCÊ ";: IF FP(0) THEN FP(0)=0: LET MA(0)=P: LET FE=-1
1150 IF FP(1) THEN FP(1)=0: LET MA(1)=P: LET FE=-1
1160 IF FP(11) THEN FP(11)=0: LET MA(11)=P: LET FE=-1
1170 IF FE THEN PRINT" LARGOU SUAS ARMAS E ";
1180 PRINT"ESTÁ DESCANSANDO": LET CF=CF+2: IF CP>0 THEN LET CF=0
1190 GOTO 1560
1200 REM ************************************************
1210 REM *                     ABRA                     *
1220 REM ************************************************
1230 INPUT"O QUE";A: FOR N=0 TO 30: GOSUB 2220
1232 IF NOT EX THEN NEXT N: GOTO 950 ELSE IF N<>15 THEN GOTO 1250 ELSE IF FP(14) THEN PRINT"VOCÊ ABRIU A ARCA"; LET FA%=-1
1234 IF RND(3)=2 THEN PRINT" E FOI SUGADO POR ELA. SUA CHAVE ESTÁ FORA."; LET FA%=0: LET P=15: LET FP(14)
=0: LET MA(14)=RND(15)-1 ELSE PRINT ELSE GOTO 1300
1240 LET FB=0: GOTO 1560
1250 IF N=2 THEN IF NOT FP(2) THEN PRINT" O LIVRO";: GOTO 1290 ELSE GOTO 1310
1260 IF N=12 THEN IF NOT FP(12) THEN PRINT" A GARRAFA";: GOTO 1290 ELSE LET FG=-1: PRINT"A GARRAFA ESTÁ ABERTA": GOTO 1560
1270 IF N=7 THEN IF NOT FP(7) THEN PRINT" O PERGAMINHO";: GOTO 1290 ELSE GOSUB 2280: LET PM$=A: GOSUB 2280: LET PM$=PM$+A: PRINT"VOCÊ LÊ: -- A PALAVRA SECRETA É ";PM$: PRINT"ELE DESAPARECE...": LET FP(7)=0: GOTO 1560
1280 PRINT"VOCÊ NÃO PODE ABRIR UM";OBJ$(N): GOTO 1560
1290 PRINT"NÇAO ESTÁ COM VOCÊ": GOTO 1560
1300 PRINT"A ARCA ESTÁ TRANCADA": GOTO 1560
1310 LET FP(2)=0: LET MA(2)=RND(20)-1: PRINT"ANTES QUE O LIVRO DESAPAREÇADE SUAS MÃOS, VOCÊ LÊ:": ON RND(5) GOTO 1320 ', 4700, 4750, 4800, 4850
1320 LET RN=RND(100): PRINT"-- O NÚMEROMÁGICO É: ";RN: GOTO 1560
1330 PRINT"-- PORQUE OS MAGOS DETESTAM VINHO.": GOTO 1560
1340 LET D(15,0)=RND(15)-1: PRINT"HÁ UMA PASSAGEM DA ARCA PARA ";LO%(D(15,0)): GOTO 1560
1350 PRINT"-- 'AKON' É A PALAVRA DAS DIREÇÕES": GOTO 1560
1360 PRINT"-- ...O PERGAMINHO QUE ESTÁ DENTRO DO DIAMANTE TEM O SEGREDO": GOTO 1560
1370 REM ************************************************
1380 REM *                     SAIA                     *
1390 REM ************************************************
1400 IF P<>15 THEN GOTO 1420 ELSE IF RND(3)=2 THEN LET MA(15)=19: PRINT"ERA UMA EMBSCADA..." ELSE IF RND(5)<3 THEN LET MA(15)=RND(15)-1: PRINT"A ARCA SE MOVEU'
1410 LET FB=0: LET P=MA(15): GOTO 1560
1420 IF P=18 THEN LET P=MA(4): LET FB=0: GOTO 1560
1440 PRINT"O QUE VOCÊ QUER DIZER COM 'SAIR'?": GOTO 1560
1450 REM ************************************************
1460 REM *                    ENTRE                     *
1470 REM ************************************************
1480 INPUT"ONDE";A: FOR N=0 TO 19: IF A<MID$(LO$((N),3,LEN(A))) THEN NEXT N: GOTO 950
1490 IF N<15 THEN GOTO 440 ELSE IF N=15 THEN IF FA% THEN LET FB=0: LET P=N: GOTO 1560 ELSE GOTO 1300
1500 IF N=18 THEN LET FP(4)=0: LET MA(4)=P: LET FB=0: LET P=N: GOTO 1560
1510 IF N=16 THEN LET FB=0: LET P=N: GOTO 1560
1520 PRINT"VOCÊ NÃO PODE ENTRAR N";LO$(N): GOTO 1560
1530 REM ************************************************
1430 REM *           ACONTECIMENTOS POSSÍVEIS           *
1550 REM ************************************************
1560 FOR N=20 TO 30: IF MA(N)<>P THEN GOTO 1580 ELSE IF RND(10)>BL%(N-20) THEN GOTO 1580
1570 PRINT"O ";OBJ$(N);" O ATACA!": LET CB=0: GOSUB 1850
1580 NEXT N
1590 FOR N=20 TO 30: IF FB>0 OR RND(6)<4 OR MA(N)<0 THEN GOTO 1620 ELSE LET K=MA(N): LET MA(N)=D(MA(N),RND(2)-1): IF MA(N)=-1 THEN LET MA(N)=K: GOTO 1620
1600 IF P=K THEN PRINT"O";OBJ$(N);" VAI PARA ";LO$(MA(N))
1610 IF MA(N)=P THEN PRINT"UM";OBJ$(N);" APARECE VINDO D";LO$(K)
1620 NEXT N
1630 IF T>30 AND MA(24)=-1 THEN IF RND(10)=7 THEN PRINTVO$;"UM GRITO ESTRANHO...": LET MA(24)=RND(15)-1
1640 IF TM<1 THEN IF MA(18)=P AND TM=0 THEN TM=RND(30)+20: PRINTVO$;":- NÃO DEVERIA TER ENTRADO AQUI, MORTAL" ELSE ELSE LET TM=TM-1
1645 IF TO=0 THEN LET TM=-1: PRINTVO$;":- DEMOREI A CHEGAR, MORTL, MAS AGORA VOCÊ PAGARÁ POR TER INVADIDO O SANTUÁRIO DE AKERNAAK!": LET MA(25)=P: LET BL%(5)=7
1650 IF TE>0 THEN LET TE=TE-1: IF TE=0 THEN LET MA(0)=RND(20)-1
1660 IF PT>100 AND MA(26)=-1 THEN IF RND(10)=1 THEN PRINTVO$;"--VOCÊ JÁ CAUSOU MUITOS PROBLEMAS, VERME. SURGE UM GUERREIRO.": LET MA(26)=P: GOTO 1560
1670 IF P=MA(19) THEN IF RND(3)=1 THEN PRINT"A ESFERA EXPLODE.": LET S=-RND(7): GOSUB 2110: LET MA(19)=-1 ELSE IF RND(4)=3 THEN PRINT"A ESFERA BRILHA FORTEMENTE...": LET P=19
1680 IF P=MA(16) AND NOT FP(13) THEN IF RND(7)=5 THEN PRINT"VO$;"-EU, A ESTATUA, SIMBOLO DE ANIK, DOU-LHE UM PRESENTE...": LET MA(13)=P
1690 IF MA(26)=P AND FP(13) AND BL%(6)>0 THEN PRINTVO$;"-EU, O GUERREIRO DE ANIK, SAÚDO O PORTADOR DO CETRO": LET BL%(6)=0
1700 IF NOT FG OR P<>MA(25) THEN GOTO 1720 ELSE PRINTVO$;"-- FRAAAAGH! NYOK! O CHEIRO DO VINHO ": IF RND(4)=3 THEN PRINT"ENFURECE O MAGO.": LET BL%(5)=9 ELSE PRINT"ENFRAQUECE O MAGO.": LET CF%(5)=CF%(5)-4
1710 PRINT"ELE QUEBRA SU";OBJ$(12);: LET FP(12)=0: LET FG=0
1720 IF T>60 AND MA(17)=-1 THEN IF RND(5)=1 THEN LET MA(17)=RND(15)-1
1730 IF PT>200 AND MA(28)=18 THEN IF RND(8)=3 THEN PRINTVO$;"--ATÉ AGORA VOCÊ ENFRENTOU OS APRENDIZES, MEU CARO...";: LET BL%(8)=7: LET MA(28)=P
1740 IF FP(6) AND MA(28)=P THEN PRINT"SEU ESPELHO DRENA UM POUCO DA MAGIA DO MESTRE": LET CF%(8)
=CF%(8)-2
1750 IF P=14 THEN IF RND(10)=8 THEN PRINT"DO ALTO DO MURO SURGE UMA LUZ CINZA.;: LET P=15: LET FB=0
1760 IF MA(17)<>-1 THEN IF RND(10)=6 THEN PRINTVO$;":-";OBJ$(17);" ESTÁ N";LO$(MA(17))
1770 IF RN=0 AND P=7 THEN IF RND(20)=2 THEN LET RN=RND(100): PRINT"VOCÊ L}Ê NUMA CARTA DE BARALHO: O No. MÁGICO É";RN
1780 IF CF<0 THEN LET CF=CF+1: IF FP(11) THEN IF CF<-1 THEN PRINT"VOCÊ SE RECUPERA MAIS DEPRESSA, FRAÇAS A SEU ELMO MÁGICO": LET CF=CF+2
1790 LET FT=0
1800 LET T=T+1: IF T/10=INT(T/10) THEN PRINT"TEMPO =";T;" MINUTOS"
1810 GOTO 300
1820 REM ************************************************
1830 REM *                  SUB DE LUTA                 *
1840 REM ************************************************
1850 IF N=0 THEN GOTO 2090 ELSE LET FB=N: IF FT THEN PRINTVE$;"TOCANDO A FLAUTA": GOTO 880: ELSE PRINT"VOCÊ O ATACA COM ";: IF FP(0) THEN PRINT"SUA ESPADA";: LET CB=CB+2 ELSE PRINT"SOCOS E PONTAPÉS";
1860 IF FP(1) THEN LET CB=CB+1:PRINT" E SE DEFENDE COM SEU ESCUDO" ELSE PRINT
1870 IF FP(3) AND N>26 THEN PRINT"SUA CAPA O PROEGE DOS GOLPES MÁGICOS MAIS FORTES DO";OBJ$(N): LET CB=CB+(N-26)*2
1880 ON N-19 GOTO 1890, 1920, 1930, 1950, 1990, 2010, 2030, 2040, 060, 2070, 2080
1890 IF RND(4)=3 THEN PRINT"ELE SE ASSUSTA COM SUA FÚRIA E DESAPARECE EM MEIO A UMA FUMAÇA VERDE.": LET FB=0 ELSE GOTO 1910
1900 LET K=RND(15)-1: IF K<>MA(20) THEN LET MA(20)=K: RETURN ELSE GOTO 1900
1910 GOTO 2090
1920 GOTO 2090
1930 IF FT THEN PRINT"ELE FICA ENFEITIÇADO COM A EXÓTICA MÚSICA QUE SAI DA SUA FLAUTA NIDDUN E SE ACALMA.": LET FB=0: RETURN
1940 GOTO 2090
1950 IF RND(4)=2 THEN PRINT"ELE GESTICULA E TUDO ESCURECEMOMENTANEAMENTE": IF FP(9) THEN LET CB=CB+2: PRINT"MESMO COM SUA LANTERNA É DIFÍCIL ENXERGAR" ELSE LET CB=CB-5 ELSE GOTO 1970
1960 GOTO 2090
1980 GOTO 2090
1990 IF RND(3)=1 THEN PRINT"O PITON O ENCARA, TENTANDO ROUBAR SUA ENERGIA": LET CF=CF-RND(3): RETURN
2000 GOTO 2090
2010 IF RND(5)=3 THEN PRINT"ELE FAZ UM GESTO ESTRANHO.": IF FP(0) THEN PRINT"SUA ESPADA DERRETE...": LET FP(0)=0: LET TE=RND(5)+10: RETURN ELSE PRINT"VOCÊ SENTE TONTURAS...": LET CB=CB-2
2020 GOTO 2090
2030 GOTO 2090
2040 IF RND(4)=4 THEN LET D(P,0)==1: LET D(P,1)=-1: LET MA(27)=RND(15)-1: PRINTVO$;"A GARGALHADA DO";OBJ$(N);":-MORTAL, VOCÊ ME SOLTOU, MAS CONSTRUIU SUA PRISÃO... ELE VAI PARA ";LO$(MA(27)): LET FB=0: RETURN
2050 GOTO 2090
2060 GOTO 2090
2070 GOTO 2090
2080 GOTO 2090
2090 LET S=CF-CF%(N-20)+CB-T/50-(N-20)*2+RND(5)
2100 IF S<-7 THEN PRINT"INFELIZMENTE MEU CARO, VOCÊ MORRE...": GOTO 2270
2110 IF S<-4 THEN PRINT"VOCÊ FOI ATINGIDO SERIAMENTE": LET CF=CF-4: GOTO 2210
2120 IF S<1 THEN PRINT"VOCÊ FOI ATINGIDO": LET CF=CF-2: GOTO 2210
2130 LET FB=0
2140 IF S<4 THEN PRINT"VOCÊ O ATINGIU": LET CF%(N-20)=CF%(N-20)-1: GOTO 2210
2150 IF S<7 THEN PRINT"VOCÊ O ATINGIU GRAVEMENTE": LET CF%(N-20)=CF%(N-20)-3:GOTO 2210
2160 PRINT"VOCÊ O MATOU": LET MA(N)=-2: LET PT=(N-19)*20+PT
2170 IF N=23 THEN IF RND(3)=1 THEN PRINT"UMA LUZ ESTRANHA SURGE, O ELFO ESTÁ RESSUCITADO. E FURIOSO!!!": LET CF%(3)=5: LET MA(N)=P ELSE PRINT"NO LUGAR DO CORPO DO ELFO APARECE UMA ESFERA": LET MA(19)=P
2180 IF N=28 OR N=29 THEN PRINT"VOCÊ CAI INCONSCIENTE... AO ACORDAR, VOCÊ PERCEBE QUE NÃO ESTÁ MAIS N";LO$(P);".";VO$":- QUEM OUSA DESAFIAR YDYAK, O DRAGÃO, ATACANDO SEUS SERVOS?": LET P=17: LET MA(30)=17
2190 IF N<>30 THEN GOTO 2210
2200 PRINT"O DRAGÃO SOME NUM VÓRTICE... SUA CASA ESTÁ LIVRE DA BARREIRA. VOCÊ VENCEU!": GOTO 2270
2210 RETURN
2220 LET EX=(MID$(OBJ$(N),2,LEN(A))=A OR MID(OBJ$(N),3,LEN(A))=A) AND (MA(N)=P OR FP(N)): RETURN
2230 INPUT"VOCÊ QUER DESISTIR DO JOGO?";A: IF A="SIM" THEN GOTO 2270 ELSE GOTO 1560
2240 REM ************************************************
2250 REM *                  FIM DO JOGO                 *
2260 REM ************************************************
2270 LET PT=PT-FP(4)*100-FP(5)*30-FP(8)*15-FP(13)*50: PRINT"O JOGO TERMINOU. VOCÊ FEZ ";PT;" PONTOS;": INPUT"VOCÊ QUER RECOMEÇAR?";A: IF A="SIM" THEN RUN ELSE END
2280 LET A=CHR$(RND(3)+65)+CHR$(61+RND(3)*4)+CHR$(73+RND(5))+CHR$(61+RND(3)*4)+CHR$(75+RND(3)): RETURN
